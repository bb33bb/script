延迟变量详解（通俗解释）
:: by 随风 @bbs.bathome.cn  2007-12-14
变量延迟  setlocal EnableDelayedExpansion
一个让大多数新手头痛的问题，网上教程虽多，但多半都是看不懂的，里面的专业术语太多。
以 cn-dos 联盟的willsort的这篇教程为例，（个人认为是解释的极具权威和专业的）
http://www.cn-dos.net/forum/viewthread.php?tid=20733&fpage=1&highlight=%E5%BB%B6%E8%BF%9F
但可能就是因为专业，所以才看不懂，因为学cmd批处理的并不一定都是学计算机专业的。
这个鬼东西确实不太好理解，在下也是摸爬滚打多时，总结出一点点经验，现用通俗的方法
解释出来，希望能给新手些帮助，老鸟们见笑了，若有不对的地方，欢迎指出。
言归正传、 
在什么时候需要延迟变量，和该如何引用延迟变量，我想这才是大多数新手迫切想要知道的问题。
耐心看完下面的内容，我想对你应该是有帮助的。
要想了解延迟变量，首先你要明白什么是“复合语句”
好像又来了个“专业”名词，别急，这个超好理解。
所谓“复合语句”就是指一对()里的所有命令。比如for的do后面
如：
for /f "delims=" %%i in (a.txt) do (
      set var=%%i
      echo %%i
      set num=%%i
)
这里do后面的三句命令，在一对()里面，这就叫“复合语句”，当然不止for 还有if 等等。。
如： 
if "%var%"=="abc" (
   echo ok
   set lis=123
)
反正就是凡是()里的所有命令，就叫“复合语句”
另外：这也是复合语句 set abc=123&echo %abc%
没错，通过管道命令&连接起来的命令，也是复合语句。
好，了解了复合语句，现在开始讲延迟变量，也就说，在复合语句中才要使用延迟变量。
我们先不去理解什么叫“变量的扩展”这玩意叫法太专业，我到现在都不太明白，
我们只要知道在什么时候需要使用延迟变量，如何才能正确提取到我们需要的变量就可以了，
这才是我们的目的。
cmd在处理“复合语句”的时候，如果“复合语句”中用到了变量，
会把变量的值当作复合语句之前变量的值来引用。如果在此之前变量没有被赋值，就把它当成空值。
呵呵，有点绕口。看个例子
例1：
复制内容到剪贴板代码:
@echo off
for /l %%i in (1 1 10) do (
    set var=%%i
    echo %var%
)
pause
运行上面的代码，显示什么？显示10个echo处于关闭状态。
按照逻辑，var的值应该依次是 1、2、3........10 才对啊！
这就是因为没有开启 延迟变量 的缘故，cmd把var的值当作复合语句之前的值来引用，
而再本例中，复合语句之前并没有给var定义，所以var的值是空的，所以会显示10个echo处于关闭状态。
再看例2：
复制内容到剪贴板代码:
@echo off
set var=abc
for /l %%i in (1 1 10) do (
    set var=%%i
    echo %var%
)
pause
运行上面的代码，会显示什么，大家应该知道了吧？
再看例3：
复制内容到剪贴板代码:
@echo off
set var=abc
for /l %%i in (1 1 5) do (
    set var%%i=%%i
    echo %var%
)
echo %var1% %var2% %var3% %var4% %var5%
pause
运行上面的代码后，复合语句中所赋的值全部显示出来了，这说明什么呢？
说明，在复合语句中，并不是没有给变量赋值，只是你若没有开启延迟变量，
你就没法在复合语句中提取到它，要等复合语句运行完毕后，才能提取到。
变量的表示方法：两种：  1、%var%   2、!var!
第一种表示方法，大家都知道，第二种就是引用 延迟的变量。
在开启了延迟变量的情况下，如果在复合语句之外，用哪种方法表示都可以。
但是你若要在复合语句中引用复合语句即时得到的变量，就要用第二种方法。看例子
例4：
复制内容到剪贴板代码:
@echo off
setlocal EnableDelayedExpansion
set var=abc
for /l %%i in (1 1 10) do (
    set var=%%i
    echo %var%
    echo !var!
)
pause
注意：例子中有两个echo 一个是显示 %var% 一个是显示 !var!
结果很明白了，%var% 显示的结果是复合语句之前变量var的值，
而 !var! 显示的就是复合语句中即时得到的值。
再看例5
复制内容到剪贴板代码:
@echo off
setlocal EnableDelayedExpansion
for /l %%i in (1 1 5) do (
    set var%%i=%%i
)
echo %var1% %var2% %var3% %var4% %var5%
echo !var1! !var2! !var3! !var4! !var5!
pause
这个例子说明什么，不用再解释了吧？
说明在开启了延迟变量的情况下，且在复合语句之外，用两种方法都可以表示变量。
就说到这吧。以上的解释，完全是出于个人的理解，也是为方便非专业人士理解，
解释肯定有错误的地方、就象学习英语时，为方便记忆，用汉字的读音来作解释一样。
呵呵，是一种“偏门”各位新手千万不要把以上所说的当成是“真理”，否则就变成是“误人”了