//RUNAS//github优化
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="github.com 网速优化工具";right=690;bottom=277;max=false)
winform.add(
cmbIpCidrs={cls="combobox";left=69;top=131;right=348;bottom=157;edge=1;items={};mode="dropdown";z=1};
editIpCurrent={cls="edit";left=138;top=226;right=386;bottom=250;edge=1;z=5};
plusExploreHosts={cls="plus";text='\uF07C 浏览hosts文件';left=470;top=224;right=627;bottom=255;bgcolor=-6371181;font=LOGFONT(h=-15;name='FontAwesome';charset=0);notify=1;z=7};
plusUpdateDns={cls="plus";text='\uF0AD 更改 github.com 域名解析到选定IP';left=136;top=178;right=525;bottom=209;bgcolor=-6371181;font=LOGFONT(h=-15;name='FontAwesome';charset=0);notify=1;z=3};
plusUpdateIps={cls="plus";text='\uF021 获取最新IP列表 / 测速';left=365;top=128;right=627;bottom=159;bgcolor=-6371181;font=LOGFONT(h=-15;name='FontAwesome';charset=0);notify=1;z=2};
static={cls="static";text='本工具使用github官网提供的IP地址接口获取最新IP列表,\n建议使用git clone测试网速,网速不佳使用此工具修改IP以后，\n然后在git命令行窗口按Ctrl+C断开（不要直接关窗口），再按键盘上的\u2191回到git clone，再次回车重复执行测试，请耐心测试，有时候提速是非常明显的。\n\n注意：从codeload.github.com下载zip的速度与此无关';left=65;top=12;right=599;bottom=103;transparent=1;z=4};
static2={cls="static";text="当前指向IP:";left=47;top=228;right=131;bottom=248;align="right";transparent=1;z=6}
)
/*}}*/

winform.cmbIpCidrs.onListChange = function(){  
	winform.setTimeout( 
		function(){
			var ip = string.match(winform.cmbIpCidrs.selText,"\d+\.\d+\.\d+\.\d+");
			winform.plusUpdateDns.text = "更改 github.com 域名解析到:" + ip
		}
	);	
}

import wsock;
import fsys.config;
config  = fsys.config( io.appData("/aardio/github-ip-tools/") ); 
winform.plusUpdateIps.oncommand = function(id,event){
	winform.plusUpdateIps.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	winform.text = "请稍候，正在测试github.com所有可用IP的响应速度"

	var metaInfo = win.invoke(
		function(){
			import win;
			import web.rest.jsonClient;
			var http = web.rest.jsonClient();
			var github = http.api("https://api.github.com/")
			
			return github.meta.get() :  { git = {
    			"192.30.252.0";
    			"185.199.108.0";
    			"140.82.112.0";
    			"13.114.40.48";
    			"13.229.188.59";
    			"13.234.176.102";
    			"13.234.210.38";
    			"13.236.229.21";
    			"13.237.44.5";
    			"13.250.177.223";
    			"15.164.81.167";
    			"18.194.104.89";
    			"18.195.85.27";
    			"35.159.8.160";
    			"52.192.72.89";
    			"52.64.108.95";
    			"52.69.186.44";
    			"52.74.223.119";
    			"52.78.231.108"
			} };
		} 
	)
	 
	if(metaInfo[["git"]]){
		config.meta.data = table.map(metaInfo.git,lambda(v)..string.match(v,"^\d+\.\d+\.\d+\.\d+"))
	}
	elseif(!config.meta.data){
		winform.msgboxErr("您的操作系统版本过低")
	}
	
	var pingThread = function( ip ) {
    	
		import wsock.tcp.client; 
		var beginTick = ..time.tick()
		var tcp = wsock.tcp.client() 
		
		tcp.setTimeouts(1000,1000);
		if( ! tcp.connectTimeout(ip,80,2) ){
			return;
		};
		
		sendData =/***********
GET /  HTTP/1.1
Host: www.github.com
Connection: close
User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64)
Accept: */*;  
Accept-Language: zh-CN,zh; 
Accept-Charset:utf-8;  
***********/
		tcp.write( sendData + '\r\n\r\n' )
		
		var rep = tcp.readAll();
		if( rep && ..string.startWith(rep,"HTTP/1.1 301 Moved Permanently") ){ 
			return ip,math.round( (..time.tick() - beginTick)/1000,2)
		} 
	} 
	
	import thread.manage;
	manage = thread.manage(#config.meta.data) 

	var ipData = {}
	for(k,ip in config.meta.data){
		manage.createSuspended(pingThread,ip).onEnd = function(ip,sec){
			if(ip && sec!==null ) {
				table.push(ipData,ip + " 响应速度:" + sec + "秒");
				winform.cmbIpCidrs.items = ipData;
				winform.cmbIpCidrs.selIndex = 1;
			}
		} 
	}
	
	manage.resume();
	manage.waitClose();
	
	winform.cmbIpCidrs.items = ipData;
	winform.cmbIpCidrs.selIndex = 1;
	winform.cmbIpCidrs.onListChange();
	
	winform.editIpCurrent.text = wsock.getIp("www.github.com")

	winform.text = "github.com 网速优化工具"
	winform.plusUpdateIps.disabledText = null;
}

import fsys.hosts;
winform.plusUpdateDns.oncommand = function(id,event){
	var ip = winform.cmbIpCidrs.text;
	if(!#ip){
		winform.msgboxErr("请选选择IP地址")
		return;
	}
	
	ip = string.match(ip,"\d+\.\d+\.\d+\.\d+");
	if( ip == winform.editIpCurrent.text ) return winform.msgboxErr("域名之前已经解析到该IP地址")
	
	winform.plusUpdateDns.disabledText = "正在更新IP"
	 
	fsys.hosts.ownCacls();
	fsys.hosts.update(
		["www.github.com"] = ip;
		["github.com"] = ip;
	)
	
	win.delay(1000);
	winform.editIpCurrent.text = wsock.getIp("www.github.com");
	winform.plusUpdateDns.disabledText = null; 
	
	if( ip != winform.editIpCurrent.text ){
		winform.msgboxErr("修改hosts文件失败，如果hosts文件被其他软件锁定时请先解除锁定，也可以搜索网上教程试试设置文件权限")
	}
}

winform.plusExploreHosts.skin(
	background = { 
		hover = "/res/images/btn-hover.png";
		focus = "/res/images/btn-focus.jpg";
		active = "/res/images/btn-active.png";
		disabled = "/res/images/btn-disabled.png"; 
	}
)
winform.plusExploreHosts.oncommand = function(id,event){
	import process;
	process.exploreSelect(fsys.hosts.path)
}

winform.plusUpdateDns.skin({
	background={
		default=0xFF93C89E;
		hover=0xFF928BB3
	}
})

winform.plusUpdateIps.skin({
	background={
		default=0xFF93C89E;
		hover=0xFF928BB3
	}
})

winform.plusExploreHosts.skin({
	background={
		default=0xFF93C89E;
		hover=0xFF928BB3
	}
})

winform.show() 
winform.plusUpdateIps.oncommand();

win.loopMessage();