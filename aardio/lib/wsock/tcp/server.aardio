//server 服务端
import wsock.err; 
import wsock.tcp.client;
namespace wsock.tcp;

class server{
	ctor(ip,port,backlog){
		if(! ..wsock.open() ){ 
			error( ( ..wsock.err.lasterr() ),2 ) 
		}
		 
		var hSocket= ..wsock.socket(0x2/*_AF_INET*/,0x1/*_SOCK_STREAM 建立流式套接口*/,0x6/*_IPPROTO_TCP*/ )
		if( hSocket = _INVALID_SOCKET ){ 
			..wsock.close() //关闭wsock
			return null,..wsock.err.lasterr();
		} 
		this.hSocket = hSocket;
		this._serverAddress = ..wsock.sockaddr_in();  
		
		this.bind = function(ip,port){
			var service = ..wsock.sockaddr_in( ip,port : 0 ); 
			service.sin_family = 0x2/*_AF_INET*/ 
			if ..wsock.bind( this.hSocket,service,..raw.sizeof(service)) =-1/*_SOCKET_ERROR*/	{ 
				return null, ..wsock.err.lasterr() ;
			} 
			..wsock.getsockname(this.hSocket, this._serverAddress,..raw.sizeof(this._serverAddress) );
			return true;
		}
		this.listen = function(backlog){ 
			if( ..wsock.listen(this.hSocket,backlog : 5 )=-1/*_SOCKET_ERROR*/){
				return null, ..wsock.err.lasterr() ;
			}
			return true;
		};
		
		var ok,err = this.bind (ip,port);
		if( ! ok ){
			..wsock.closesocket(this.hSocket); 
			..wsock.close() //关闭wsock
			return null,err;
		}
		
		var ok,err = this.listen ( backlog );
		if( ! ok ){
			..wsock.closesocket(this.hSocket); 
			..wsock.close() //关闭wsock
			return null,err;
		}
		
		
		this.getLocalIp = function(){
			return ..wsock.inet_ntoa(this._serverAddress.sin_addr),..wsock.ntohs( this._serverAddress.sin_port );
		};
	};
	close = function(){
		if( this.hSocket ){
			..wsock.closesocket(this.hSocket); 
			..wsock.close();
			this.hSocket = null;
		}
	};
	forever = function(handle){
		if( type(handle) != type.function ) error("参数@1必须是一个函数对象",2);
	 
		while( true ){
			var acceptSocket = ..wsock.accept(this.hSocket)
			if( acceptSocket!= _INVALID_SOCKET ){ 
				this.lastRequestSocket = acceptSocket;
				if( handle(acceptSocket) ===  false ){
					break;
				}
			}
		}
		 
		this.close();
	};
};

namespace server{
	getFreePort =  ..wsock.getFreePort;
}

/*
http://www.iana.org/assignments/service-names-port-numbers/service-names-port-numbers.xhtml
*/

/**intellisense()
wsock.tcp.server = TCP服务端
wsock.tcp.server(.(IP,端口,请求队列大小) = 创建TCP服务端,所有参数可选,\n\n如果不写IP，则默认设为"0.0.0.0"也即监听本机所有IP,访问此服务端也不限制IP\n限制仅本机可以访问建议写127.0.0.1\n\n端口为0或省略则自动查找1025以后的空闲端口\n注意0-1023为系统通用服务保留端口,\n1024-49151为用户服务端口,其中大约%9已由IANA注册分配\n49152-65535为私有或临时端口
wsock.tcp.server() = !stdwsocktcpserver.
!stdwsocktcpserver.bind(.(IP,端口) = 绑定IP端口
!stdwsocktcpserver.listen(.(请求队列大小) = 监听指定的IP端口
!stdwsocktcpserver.close() = 关闭TCP服务端
!stdwsocktcpserver.getLocalIp() = 返回当前绑定的IP,端口号
!stdwsocktcpserver.forever(回调函数) = @.forever(\n	function(acceptSocket){\n		var request = wsock.tcp.client(,acceptSocket)\n	}\n)	
!stdwsocktcpserver._serverAddress = 服务端监听地址\n!sockaddr_in.
end intellisense**/