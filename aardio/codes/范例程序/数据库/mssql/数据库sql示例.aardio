//增、删、改、查

import console;
import sqlServer;

//打开数据库
var db= sqlServer( 
    ["Data Source"]= "IP地址,服务端口";
    ["Database"]= "数据库名";
    ["User ID"]  = "用户名";
    ["Password"]= "用户密码"; 
)
 
//创建表
if(!db.existsTable("dbTest") ){
	db.exec("create table dbTest (url char(130), comment char(120))")
}

//使用命令参数 - 参数化可避免SQL注入
var cmd = db.createCommand( "INSERT INTO dbTest(url,comment) VALUES(@url,@comment)" );  

cmd.bind( 
	url = "http://bbs.aardio.com";
	comment = "hi!";
)

cmd.execute(); //执行命令

//增 - 调用 access.formatParameter() 格式化命名参数生成SQL语句
db.exec( "INSERT INTO dbTest(url,comment) VALUES(@url,@comment)",{
	url = "http://bbs.aardio.com";
	comment = "字符串包含'单引号' 测试一下"
} )

//删 - 自动调用 string.format() 函数格式化SQL语句,简单拼接字符串应避免包含单引号
db.exec("delete from dbTest where url='%s'","http://bbs.aardio.com");

//改
db.exec("update dbTest set url='%s' WHERE comment='%s' ","http://www.aardio.com","这是说明")
 
//查
for(rs in db.each("SELECT * from dbTest") ){ 
	console.log( rs("url").value,rs("comment").value )  
}
 
//将查询结果转换为普通数组
var tab = db.getTable("SELECT * FROM dbTest");
console.dump(tab);

//关闭数据库连接
db.close();

console.pause();