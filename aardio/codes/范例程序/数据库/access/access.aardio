//mdb数据库
//下面的代码仅使用系统组件,不需要安装ACCESS软件

import access;
import console;

//打开数据库,如果不存在就自动创建
var db = access("/test.mdb") //也可以直接写数据库连接串 http://www.connectionstrings.com/excel/

//创建表
if(!db.existsTable("homepage2") ){
	db.exec("CREATE TABLE homepage2 (url char(30), comment char(20))")
}

//使用命令参数 - 参数化可避免SQL注入
var cmd = db.createCommand( "INSERT INTO homepage2(url,comment) VALUES(@url,@comment)" );  

//设置所有参数的值
cmd.bind( 
	url = "http://www.aardio.com";
	comment = "hi!";
)

cmd.execute() //执行命令

//增 - 调用 access.formatParameter() 格式化命名参数生成SQL语句
db.exec( "INSERT INTO homepage2(url,comment) VALUES(@url,@comment)",{
	url = "http://www.aardio.com";
	comment = "字符串包含'单引号' 测试一下"
} )

//删 - 自动调用 string.format() 函数格式化SQL语句,简单拼接字符串应避免包含单引号
db.exec("DELETE * FROM homepage2 WHERE url='%s'","http://www.aardio.com");
//注意 access 需要写 delete *, 其他数据库不写星号。

//改
db.exec("UPDATE homepage2 SET url='%s' WHERE comment='%s' ","http://www.aardio.com","这是说明")
 
//查
for(rs,fields in db.each("SELECT * FROM homepage2") ){ 
	console.log( rs("url").value,rs("comment").value )  
}
 
//将查询结果转换为普通数组
var tab = db.getTable("SELECT * FROM homepage2");
console.dump(tab);

//关闭数据库连接
db.close();
console.pause();