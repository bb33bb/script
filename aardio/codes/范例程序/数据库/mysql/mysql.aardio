import console;
import mysql.client;

console.log("正在连接数据库服务器...");
thread.invoke( function() 
	while(! thread.get("db4free.net:end")){
         io.stdout.write(">");sleep(100);
    }  
)

var dbMysql,err = mysql.client(
    server = "db4free.net"; //数据库服务器,可省略默认为localhost
    uid = "aardio";//用户名,可省略默认为root
    pwd = "aardio.com";
); 

thread.set("db4free.net:end",true)
console.clearScreen ();
if(!dbMysql){ 
    console.log("连接数据库失败",err);
    console.pause();
    return;
}

//选择数据库
dbMysql.selectDb("aardio")  

//执行SQL语句,注意mysql表名、字段名可包含于反引号内(用键盘左上角ESC下方的按键内)
dbMysql.exec("CREATE TABLE IF NOT EXISTS `library` (
    `name` varchar(100) NOT NULL, `auditing` tinyint(1) DEFAULT '0',`bytes` BLOB,PRIMARY KEY (`name`),UNIQUE KEY `name` (`name`)
) ENGINE=MyISAM DEFAULT CHARSET=utf8;");

var dbTable,err  = dbMysql.listTables() 
for tbl in dbTable.each(){
    console.log("发现数据表:",tbl);
}
 
//执行SQL语句,使用命名参数
var ok,err = dbMysql.exec("INSERT `library` VALUES('测试',@num,@str)",{
    num = 123;
    str = "测试:'这是字符串!'命名参数可以自动处理字符串转义";
} ) 
console.log(ok,err);

//查询数据并返回记录集
var result = dbMysql.query("SELECT * FROM `library`");
for name,auditing,bytes in result.each(){ 
    console.log( name,auditing,bytes ); //逐行输出所有记录
}

//移除数据库
dbMysql.exec("DROP TABLE IF EXISTS `library`");
console.pause();