import win.ui;
/*DSG{{*/
var winform = win.form(text="贴吧抓取﻿工具";right=1141;bottom=791;bgcolor=16777215)
winform.add(
btnGet={cls="button";text="抓取";left=246;top=4;right=336;bottom=32;dl=1;dt=1;z=2};
cmbKeyWord={cls="combobox";left=85;top=5;right=232;bottom=31;dl=1;dt=1;edge=1;items={};mode="dropdown";z=4};
lbKeyWord={cls="static";text="吧名:";left=6;top=8;right=62;bottom=30;align="right";dl=1;dt=1;transparent=1;z=3};
static={cls="static";left=10;top=46;right=1120;bottom=788;db=1;dl=1;dr=1;dt=1;transparent=1;z=1}
)
/*}}*/

import web.form;
var wb = web.form(winform.static);

import string.html;
import web.rest.client;

var http = web.rest.client()
var tieba = http.api("http://tieba.baidu.com/f?ie=utf-8&kw={kw}","GET"
    ,`id="pagelet_html_frs-list/pagelet/thread_list" style="display\:none;">\<\!--(.+?)--\>`)
    
winform.cmbKeyWord.items = {"吧名1";"吧名2"}
winform.btnGet.oncommand = function(id,event){
    var kw = winform.cmbKeyWord.text;
    if(!#kw){
        return winform.msgboxErr("请输入关键字")
    }
    
    var html = tieba[kw](pn=0) 
    var htmlDoc = string.html( html )
    var links = htmlDoc.queryEles(["class"]="j_th_tit";["tagName"]="a")
    
    for i,a in table.eachIndex(links){
        a.href = "https://tieba.baidu.com" + a.href; 
    }	
 
    var htmlNew =  /* 
    <!doctype html>
    <html>
    <head>
        <meta charset="utf-8">
        <meta http-equiv="X-UA-Compatible" content="IE=edge" />
        <style type="text/css">
        html,body{ height:100%; margin:0; } 
        </style> 
    </head>
    <body> 
    </body>
    */
    
    var htmlNewDoc = string.html(htmlNew);
    var body = htmlNewDoc.queryEle(tagName="body");
    for(i=1;#links;1){
        body.pushElement(links[i])
        body.pushElement( tagName =  "br")
    }
     
    wb.html = tostring(htmlNewDoc)
}

import process;
wb.translate = function( url ){
    import process;
    process.openUrl(url)
}

wb.NewWindow3 = function(ppDisp,cancel,dwFlags,bstrUrlContext,bstrUrl ) {  
    return ppDisp,true;
};

winform.show();
win.loopMessage();